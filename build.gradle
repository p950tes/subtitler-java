plugins {
	id 'java'
	id 'application'
    id 'org.graalvm.buildtools.native' version '0.10.2'
}
repositories {
	mavenCentral()
}

group = 'se.p950tes'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = JavaVersion.VERSION_21
}

application {
	mainClass = 'se.p950tes.subtitler.SubtitlerApplication'
}
jar {
	duplicatesStrategy = 'warn'
	manifest.attributes 'Main-Class': application.mainClass
    
    from sourceSets.main.output
    from {
    	configurations.compileClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    exclude 'META-INF/*.RSA', 'META-INF/*.DSA', 'META-INF/*.SF', 'META-INF/versions/*/module-info.class'
}

dependencies {
	implementation 'org.apache.commons:commons-lang3:3.14.0'
	
	annotationProcessor 'info.picocli:picocli-codegen:4.7.6'
	implementation 'info.picocli:picocli:4.7.6'
	implementation 'info.picocli:picocli-jansi-graalvm:1.2.0'

	testImplementation 'org.junit.jupiter:junit-jupiter:5.10.2'
	testImplementation 'org.mockito:mockito-junit-jupiter:5.12.0'
	testImplementation 'org.hamcrest:hamcrest:2.2'
	testImplementation 'org.assertj:assertj-core:3.26.0'

	testRuntimeOnly 'org.junit.platform:junit-platform-launcher:1.10.2'
}

tasks.named('test') {
	useJUnitPlatform()
}

graalvmNative {
	metadataRepository.enabled = true
	binaries {
		main {
			// verbose = true
			//quickBuild = true // OBS Only during dev
			sharedLibrary = false
		}
	}
}
